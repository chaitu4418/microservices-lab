buildscript {
    ext {
        springBootVersion = '1.2.0.RC2'
        springCloudVersion = '1.0.0.M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  repositories {
      mavenLocal()
      mavenCentral()
      maven { url "https://repo.spring.io/snapshot" }
      maven { url "https://repo.spring.io/milestone" }
      maven { url "http://m2.neo4j.org/content/repositories/releases" }
  }
}

subprojects {
  apply plugin: 'spring-boot'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.cloud:spring-cloud-pcf-connector:1.1.1.BUILD-SNAPSHOT")
    compile("org.springframework.cloud:spring-cloud-cloudfoundry-connector:1.1.1.BUILD-SNAPSHOT")
    compile("org.springframework.cloud:spring-cloud-spring-service-connector:1.1.1.BUILD-SNAPSHOT")
    compile("org.springframework.cloud:spring-cloud-starter-hystrix:${springCloudVersion}")
    compile("org.springframework.cloud:spring-cloud-starter-eureka:${springCloudVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test")
  }
}

project(':catalog') {
  jar {
      baseName = 'catalog'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    runtime("org.hsqldb:hsqldb")
    runtime("postgresql:postgresql:9.1-901-1.jdbc4")
    runtime("mysql:mysql-connector-java:5.1.25")
  }
}

project(':configserver') {
  jar {
      baseName = 'configserver'
      version = '0.0.1-SNAPSHOT'
  }
}

project(':eureka') {
  jar {
      baseName = 'eureka'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
      compile("org.springframework.cloud:spring-cloud-starter-eureka-server:${springCloudVersion}")
  }
}

project(':gateway') {
  jar {
      baseName = 'gateway'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
      compile("org.springframework.cloud:spring-cloud-starter-zuul:${springCloudVersion}")
  }
}

project(':hystrixDashboard') {
  jar {
      baseName = 'hystrixDashboard'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
      compile("org.springframework.cloud:spring-cloud-starter-hystrix-dashboard:${springCloudVersion}")
  }
}

project(':recommendations') {
  jar {
      baseName = 'recommendations'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
    compile("org.springframework.data:spring-data-neo4j-rest:3.2.1.RELEASE")
    compile("org.hibernate:hibernate-validator")
  }
}

project(':reviews') {
  jar {
      baseName = 'reviews'
      version = '0.0.1-SNAPSHOT'
  }

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
